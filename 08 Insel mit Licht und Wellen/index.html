<!DOCTYPE HTML>
<html>
	<head>
		<meta charset="utf-8"/>
		<title>07 Insel</title>

		<script id="vertex-shader" type="x-shader/x-vertex">
			attribute vec3 vPosition;
			attribute vec4 vColor;
			attribute vec2 vTexture;
			attribute vec3 vNormal;

			uniform mat4 mProjection;
			uniform mat4 mModel;
			uniform mat4 mView;
			uniform mat3 mNormal;
			uniform float fTimer;

			varying vec4 fvModelPosition;
			varying vec4 fvColor;
			varying vec2 fvTexture;
			varying vec3 fvNormal;
			varying mat3 TBN;

			void main() {
				fvColor = vColor;
				fvTexture = vTexture;

				// fvNormal = mNormal * vec3(normalize(vec4(vPosition, 1)));
				fvNormal = mNormal * vNormal;

				vec3 tangent;
				vec3 t1 = cross(fvNormal, vec3(0.0, 0.0, 1.0));
				vec3 t2 = cross(fvNormal, vec3(0.0, 1.0, 0.0));

				vec3 T = normalize(mNormal * (length(t1) > length(t2) ? normalize(t1) : normalize(t2)));
				vec3 N = normalize(fvNormal);
				T = normalize(T - dot(T, N) * N);
				vec3 B = normalize(cross(T, N));
				TBN = mat3(T, B, N);
				

				fvModelPosition = mModel * vec4(vPosition.x, vPosition.y, vPosition.z, 1);
				

				gl_Position = mProjection * mView * fvModelPosition;
			}
		</script>
		<script id="vertex-shader-water" type="x-shader/x-vertex">
			attribute vec3 vPosition;
			attribute vec4 vColor;
			attribute vec2 vTexture;
			attribute vec3 vNormal;

			uniform mat4 mProjection;
			uniform mat4 mModel;
			uniform mat4 mView;
			uniform mat3 mNormal;
			uniform float fTimer;

			varying vec4 fvModelPosition;
			varying vec4 fvColor;
			varying vec2 fvTexture;
			varying vec3 fvNormal;
			varying mat3 TBN;
			varying vec3 normal;




			//Hier ist der Simplex-Noise-Algorithmus von https://gist.github.com/patriciogonzalezvivo/670c22f3966e662d2f83

			vec4 permute(vec4 x){return mod(((x*34.0)+1.0)*x, 289.0);}
			float permute(float x){return floor(mod(((x*34.0)+1.0)*x, 289.0));}
			vec4 taylorInvSqrt(vec4 r){return 1.79284291400159 - 0.85373472095314 * r;}
			float taylorInvSqrt(float r){return 1.79284291400159 - 0.85373472095314 * r;}

			vec4 grad4(float j, vec4 ip){
			const vec4 ones = vec4(1.0, 1.0, 1.0, -1.0);
			vec4 p,s;

			p.xyz = floor( fract (vec3(j) * ip.xyz) * 7.0) * ip.z - 1.0;
			p.w = 1.5 - dot(abs(p.xyz), ones.xyz);
			s = vec4(lessThan(p, vec4(0.0)));
			p.xyz = p.xyz + (s.xyz*2.0 - 1.0) * s.www; 

			return p;
			}

			float snoise(vec4 v){
			const vec2  C = vec2( 0.138196601125010504,  // (5 - sqrt(5))/20  G4
									0.309016994374947451); // (sqrt(5) - 1)/4   F4
			// First corner
			vec4 i  = floor(v + dot(v, C.yyyy) );
			vec4 x0 = v -   i + dot(i, C.xxxx);

			// Other corners

			// Rank sorting originally contributed by Bill Licea-Kane, AMD (formerly ATI)
			vec4 i0;

			vec3 isX = step( x0.yzw, x0.xxx );
			vec3 isYZ = step( x0.zww, x0.yyz );
			//  i0.x = dot( isX, vec3( 1.0 ) );
			i0.x = isX.x + isX.y + isX.z;
			i0.yzw = 1.0 - isX;

			//  i0.y += dot( isYZ.xy, vec2( 1.0 ) );
			i0.y += isYZ.x + isYZ.y;
			i0.zw += 1.0 - isYZ.xy;

			i0.z += isYZ.z;
			i0.w += 1.0 - isYZ.z;

			// i0 now contains the unique values 0,1,2,3 in each channel
			vec4 i3 = clamp( i0, 0.0, 1.0 );
			vec4 i2 = clamp( i0-1.0, 0.0, 1.0 );
			vec4 i1 = clamp( i0-2.0, 0.0, 1.0 );

			//  x0 = x0 - 0.0 + 0.0 * C 
			vec4 x1 = x0 - i1 + 1.0 * C.xxxx;
			vec4 x2 = x0 - i2 + 2.0 * C.xxxx;
			vec4 x3 = x0 - i3 + 3.0 * C.xxxx;
			vec4 x4 = x0 - 1.0 + 4.0 * C.xxxx;

			// Permutations
			i = mod(i, 289.0); 
			float j0 = permute( permute( permute( permute(i.w) + i.z) + i.y) + i.x);
			vec4 j1 = permute( permute( permute( permute (
						i.w + vec4(i1.w, i2.w, i3.w, 1.0 ))
					+ i.z + vec4(i1.z, i2.z, i3.z, 1.0 ))
					+ i.y + vec4(i1.y, i2.y, i3.y, 1.0 ))
					+ i.x + vec4(i1.x, i2.x, i3.x, 1.0 ));
			// Gradients
			// ( 7*7*6 points uniformly over a cube, mapped onto a 4-octahedron.)
			// 7*7*6 = 294, which is close to the ring size 17*17 = 289.

			vec4 ip = vec4(1.0/294.0, 1.0/49.0, 1.0/7.0, 0.0) ;

			vec4 p0 = grad4(j0,   ip);
			vec4 p1 = grad4(j1.x, ip);
			vec4 p2 = grad4(j1.y, ip);
			vec4 p3 = grad4(j1.z, ip);
			vec4 p4 = grad4(j1.w, ip);

			// Normalise gradients
			vec4 norm = taylorInvSqrt(vec4(dot(p0,p0), dot(p1,p1), dot(p2, p2), dot(p3,p3)));
			p0 *= norm.x;
			p1 *= norm.y;
			p2 *= norm.z;
			p3 *= norm.w;
			p4 *= taylorInvSqrt(dot(p4,p4));

			// Mix contributions from the five corners
			vec3 m0 = max(0.6 - vec3(dot(x0,x0), dot(x1,x1), dot(x2,x2)), 0.0);
			vec2 m1 = max(0.6 - vec2(dot(x3,x3), dot(x4,x4)            ), 0.0);
			m0 = m0 * m0;
			m1 = m1 * m1;
			return 49.0 * ( dot(m0*m0, vec3( dot( p0, x0 ), dot( p1, x1 ), dot( p2, x2 )))
						+ dot(m1*m1, vec2( dot( p3, x3 ), dot( p4, x4 ) ) ) ) ;

			}





			//Hier beginnt unser Code

			//Berechnet den neuen Vertex
			vec3 f(vec3 vertex){
				float amplitude = 0.2;
				float speed = 1.8;
				float pi = 3.141;
				float frequence = 400.0;				
				float simplexNoiseAmplitude = 0.2;

				float y = vertex.y + simplexNoiseAmplitude * snoise(vec4(vertex, fTimer* 0.66))+ amplitude * sin(2.0*pi*frequence * vertex.x + fTimer * speed) ;
				return vec3(vertex.x, y, vertex.z);
			}

			vec3 calculateNormal(vec3 vertex){
				float epsilon = 0.1;
				vec3 vertexEpsilonX = vec3(vertex.x + epsilon, vertex.y, vertex.z);
				vec3 vertexEpsilonZ = vec3(vertex.x, vertex.y, vertex.z + epsilon);

				vec3 Vec3XZ = f(vertex);
				vec3 Vec3EpsilonX =  f(vertexEpsilonX); 
				vec3 Vec3EpsilonZ = f(vertexEpsilonZ); 

				vec3 t = Vec3EpsilonX - Vec3XZ;
				vec3 b =  Vec3EpsilonZ - Vec3XZ;
				return normalize(cross(b,t));
			}

			void main() {
				fvColor = vColor;
				fvTexture = vTexture;				
				
				fvModelPosition = mModel * vec4(f(vPosition), 1);

				normal = calculateNormal(fvModelPosition.xyz);
				fvNormal = mNormal * normal;

				vec3 tangent;
				vec3 t1 = cross(fvNormal, vec3(0.0, 0.0, 1.0));
				vec3 t2 = cross(fvNormal, vec3(0.0, 1.0, 0.0));

				vec3 T = normalize(mNormal * (length(t1) > length(t2) ? normalize(t1) : normalize(t2)));
				vec3 N = normalize(fvNormal);
				T = normalize(T - dot(T, N) * N);
				vec3 B = normalize(cross(T, N));
				TBN = mat3(T, B, N);
				
				//vPosition.y = amplitude * sin(2*pi*frequence * vertex.x + timer * speed)

				

				gl_Position = mProjection * mView * fvModelPosition;
			}
		</script>
		<script id="fragment-shader-water" type="x-shader/x-fragment">
			precision mediump float;
			const int MAX_LIGHT_SOURCES = 100;

			varying vec4 fvModelPosition;
			varying vec4 fvColor;
			varying vec2 fvTexture;
			varying vec3 fvNormal;
			varying mat3 TBN;
			varying vec3 normal;

			const float c1 = 1.0;
			const float c2 = 0.0005;
			const float c3 = 0.000003;

			uniform int iLightSources;
			uniform vec3 vLightSources[MAX_LIGHT_SOURCES];
			uniform vec3 rgbDiffus[MAX_LIGHT_SOURCES];
			uniform vec3 rgbSpecular[MAX_LIGHT_SOURCES];

			uniform sampler2D uSampler;
			uniform sampler2D uNormalSampler;
			uniform vec3 vAmbient;
			uniform vec3 vEye;
			uniform bool bUseTexture;

			void main() {
				vec3 vDiffus = vec3(0);
				vec3 vSpecular = vec3(0);

				vec3 N = normalize(texture2D(uNormalSampler, fvTexture).rgb * 2.0 - 1.0);
				for (int i=0; i<MAX_LIGHT_SOURCES; i++) {
					if (i >= iLightSources) break;

					// Calculate fAtt
					float d = distance(vLightSources[i], fvModelPosition.xyz);
					float fAtt = min(1.0/(c1 + c2 * d + c3 * pow(d, 2.0)), 1.0);

					vec3 L = normalize(TBN * (vLightSources[i] - fvModelPosition.xyz));
					vec3 R = reflect(-L, N);
					vec3 V = normalize(TBN * (vEye-fvModelPosition.xyz));

					vDiffus += rgbDiffus[i] * fAtt * clamp(dot(N, L), 0.0, 1.0);
					vSpecular += rgbSpecular[i] * fAtt * pow(clamp(dot(R, V), 0.0, 1.0), 20.0);
				}

				if (bUseTexture) {
					gl_FragColor = texture2D(uSampler, fvTexture);
				} else {
					gl_FragColor = vec4(0.0, 0.0, 1.0, 1.0);//fvColor;
				}

				gl_FragColor.rgb *= (vAmbient + vDiffus + vSpecular);
			}
		</script>
		<script id="fragment-shader" type="x-shader/x-fragment">
			precision mediump float;
			const int MAX_LIGHT_SOURCES = 100;

			varying vec4 fvModelPosition;
			varying vec4 fvColor;
			varying vec2 fvTexture;
			varying vec3 fvNormal;
			varying mat3 TBN;

			const float c1 = 1.0;
			const float c2 = 0.0005;
			const float c3 = 0.000003;

			uniform int iLightSources;
			uniform vec3 vLightSources[MAX_LIGHT_SOURCES];
			uniform vec3 rgbDiffus[MAX_LIGHT_SOURCES];
			uniform vec3 rgbSpecular[MAX_LIGHT_SOURCES];

			uniform sampler2D uSampler;
			uniform sampler2D uNormalSampler;
			uniform vec3 vAmbient;
			uniform vec3 vEye;
			uniform bool bUseTexture;

			void main() {
				vec3 vDiffus = vec3(0);
				vec3 vSpecular = vec3(0);

				vec3 N = normalize(texture2D(uNormalSampler, fvTexture).rgb * 2.0 - 1.0);
				for (int i=0; i<MAX_LIGHT_SOURCES; i++) {
					if (i >= iLightSources) break;

					// Calculate fAtt
					float d = distance(vLightSources[i], fvModelPosition.xyz);
					float fAtt = min(1.0/(c1 + c2 * d + c3 * pow(d, 2.0)), 1.0);

					vec3 L = normalize(TBN * (vLightSources[i] - fvModelPosition.xyz));
					vec3 R = reflect(-L, N);
					vec3 V = normalize(TBN * (vEye-fvModelPosition.xyz));

					vDiffus += rgbDiffus[i] * fAtt * clamp(dot(N, L), 0.0, 1.0);
					vSpecular += rgbSpecular[i] * fAtt * pow(clamp(dot(R, V), 0.0, 1.0), 20.0);
				}

				if (bUseTexture) {
					gl_FragColor = texture2D(uSampler, fvTexture);
				} else {
					gl_FragColor = fvColor;
				}

				gl_FragColor.rgb *= (vAmbient + vDiffus + vSpecular);
			}
		</script>
		<script id="vertex-shader-skybox" type="x-shader/x-vertex">
			attribute vec3 vPosition;

			uniform mat4 mProjection;
			uniform mat4 mModel;
			uniform mat4 mView;

			varying vec3 fvPosition;

			void main() {
				gl_Position = mProjection * mView * mModel * vec4(vPosition, 1);
				fvPosition = vPosition.xyz;
			}
		</script>
		<script id="fragment-shader-skybox" type="x-shader/x-fragment">
			precision mediump float;
			const int MAX_LIGHT_SOURCES = 100;

			varying vec3 fvPosition;

			uniform samplerCube uSampler;

			void main() {
				gl_FragColor = textureCube(uSampler, fvPosition);
			}
		</script>

		<script type="text/javascript" src="common/initShaders.js"></script>
		<script type="text/javascript" src="common/gl-matrix.js"></script>
		<script type="text/javascript" src="common/constants.js"></script>
		<script type="text/javascript" src="GlCanvas.js"></script>
		<script type="text/javascript" src="GlObject.js"></script>
		<script type="text/javascript" src="Camera.js"></script>
		<script type="text/javascript" src="Cube.js"></script>
		<script type="text/javascript" src="Palm.js"></script>
		<script type="text/javascript" src="Surface.js"></script>
		<script type="text/javascript" src="Water.js"></script>
		<script type="text/javascript" src="Light.js"></script>
		<script type="text/javascript" src="Skybox.js"></script>

		<style>
			body {
				max-width: 1000px;
				margin: auto;
			}
			#gl-canvas {
				border: 1px solid #00000066;
				margin: auto;
				display: block
			}
			.slider {
				background: transparent;
				display: block;
				height: 1.2rem;
				width: 12em;
			}
			.slider::-moz-range-thumb {
				background: #5755d9;
				border: 0;
				height: 0.6rem;
				width: 0.6rem;
			}
			.slider::-moz-range-track {
				background: #f0f1f4;
				border-radius: 0.25rem;
				height: 0.25rem;
				width: 100%;
			}
		</style>
	</head>
	<body onload="init()">
		<h1>Insel</h1>

		<label for="terrain">Terrain</label>
		<input id="terrain" class="slider" type="range" min="0" max="20" value="0" oninput="increaseZIndex(this.value)" onchange="increaseZIndex(this.value)"/>
		<input id="play" type="button" onclick="rpl()" value="play" style="margin: 5px 0 10px 0"/>

		<br/>
		<label for="lichtX">Licht X-Koordinate</label>
		<input id="lichtX" class="slider" type="range" min="-100" max="100" value="0"/>

		<label for="lichtY">Licht Y-Koordinate</label>
		<input id="lichtY" class="slider" type="range" min="-100" max="100" value="0"/>

		<label for="lichtZ">Licht Z-Koordinate</label>
		<input id="lichtZ" class="slider" type="range" min="-100" max="100" value="0"/>

		<canvas id="gl-canvas" width="1024" height="768">
			If you see this, your browser doesn't support WebGL.
		</canvas>

		<div style="margin-top: 5px">
			Controls:<br>
			W - walk forward<br>
			A - strafe left<br>
			S - walk backward<br>
			D - strafe right<br>
			R - rise<br>
			F - fall
		</div>

		<script type="text/javascript" src="insel.js"></script>
	</body>
</html>
